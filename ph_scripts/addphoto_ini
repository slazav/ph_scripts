#!/usr/bin/perl

use strict;
use warnings;
use addphoto;
use Getopt::Long qw(GetOptions);


my $max_width=800;

######################################################################

sub usage{
print qq*
$0 - create template for a photoalbum

Usage: $0 <options> <photo dir>

Options:
    -h,--help           -- display help screen
    -T,--time_sort      -- sort images by time, not by name

Report bugs to <slazav\@altlinux.org>
*;
exit(1);
}

######################################################################

### get options.
my %opts;
GetOptions(
  "h|help"      => \$opts{help},
  "D|time_sort" => \$opts{time_sort},
) or usage();

usage() if $opts{help} || @ARGV<1;

my $dir=$ARGV[0];
my $file=$dir. '.txt';

## get file list (jpeg, png, tiff)
my @files = grep {/.jpe?g$/i || /.png$/i || /.tiff?$/i}
  addphoto::read_dir($dir);
@files = grep {!/(^_|\/_)/} @files;

## create thumbnails, collect information about files: date, thumbnail width
my %files;
foreach (@files) {
  my ($d, $b, $e) = addphoto::path_split($_);
  my $file =$_;

  # create thumbnail image, get its width
  my $tfile="${d}_$b$e";
  addphoto::image_resize($file, $tfile, (scale=>$addphoto::def_thscale))
    if addphoto::isolder($tfile, $file);
  my ($w, $h) = addphoto::image_size($tfile);

  # date from exif
  my $date = addphoto::get_exif($file)->{dat} || '';

  # ralative path
  my $p = $file; $p=~s|^$dir/||; # relative dir

  $files{$_} = {w=>$w, d=>$date, p=>$p};
}

rename $file, "$file.bak" if -f $file;
open OUT, "> $file" or die "Can't open $file: $!\n";
print OUT "<html><body>\n";

my $ww=0;  # current width
my $od=''; # current date

my @list = $opts{time_sort}? (sort {$files{$a}->{d} cmp $files{$b}->{d}} keys %files) :
                             (sort keys %files);
foreach (@list) {
  my $d = $files{$_}->{d};
  my $w = $files{$_}->{w};
  my $p = $files{$_}->{p};

  if ( $od ne $d){
    print OUT "\n\\h4r $d\n";
    $ww=0;
    $od=$d;
  }

  $ww+=$w;
  if ($ww>$max_width) {
    print OUT "\n";
    $ww=$w;
  }
  print OUT "\\photo $p\n";
}

print OUT "</body></html>\n";
close OUT;

`addphoto "$file" > "$dir.htm"`;
