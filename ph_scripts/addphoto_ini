#!/usr/bin/perl

use strict;
use warnings;

my $max_width=600;

######################################################################

sub usage{
print qq*
$0 - create template for a photoalbum

Usage: $0 <photo dir>

Report bugs to <slazav\@altlinux.org>
*;
exit(1);
}

## split path into full directory name + basename + last extension
## './some/path/file.e1.e2.ext' -> './some/path/' + 'file.e1.e2' + '.ext'
sub path_split($){
  $_[0]=~m&(.*\/)?((.*)(\..*)|(.*)())&;
  return (($1 or ''), ($3 or $5 or ''), ($4 or $6 or ''));
}

## Is first file older then second one or
## first file does not exist.
sub isold($$){
  return 0 if (!-f $_[1]);
  return 1 if (!-f $_[0]);
  return (stat $_[0])[9] < (stat $_[1])[9];
}

## list all files in the directory
sub read_dir{
  my @ret;
  my $dir   = shift;
  unless(opendir D, $dir){
    warn "Skipping unreadable directory $dir: $!\n";
    return;
  }
  my @list = readdir D;
  closedir D;

  foreach (@list){
    next if /^\.{1,2}$/;
    my $f = "$dir/$_";
    push(@ret, (-d $f)? read_dir($f) : $f);
  }
  return @ret;
}


######################################################################

usage() if @ARGV!=1;

my $dir=$ARGV[0];
my $file=$dir. '.txt';

## get file list (jpeg, png, tiff)
my @files = grep {/.jpe?g$/i || /.png$/i || /.tiff?$/i} read_dir($dir);
@files = grep {!/(^_|\/_)/} @files;

## collect information about files: date, thumbnail width
my %files;
foreach (@files) {
  my ($d, $b, $e) = path_split($_);
  my $file =$_;

  # create thumbnail image, get its width
  my $tfile="${d}_$b$e";
  `ph_resize "$file" "$tfile" 200 100 600` if isold($tfile, $file);

  `identify "$tfile"` =~/(\d+)x(\d+)/;
  my ($w, $h) = ($1 || 0, $2);

  # date from exif
  my $date='';
  if ($e=~/^.jp/){
    $date=`exiv2 -Pkv $file 2>/dev/null | grep '^Exif.Photo.DateTimeOriginal'`;
    $date =~ s|Exif.Photo.DateTimeOriginal\s*\([\d:]+\).*|$1|;
    $date =~ tr|:|/|;
  }

  # ralative path
  my $p = $file; $p=~s|^$dir/||; # relative dir

  $files{$_} = {w=>$w, d=>$date, p=>$p};
}

rename $file, "$file.bak" if -f $file;
open OUT, "> $file" or die "Can't open $file: $!\n";
print OUT "<html><body>\n";

my $ww=0;  # current width
my $od=''; # current date
foreach (sort keys %files) {
  my $d = $files{$_}->{d};
  my $w = $files{$_}->{w};
  my $p = $files{$_}->{p};

  if ( $od ne $d){
    print OUT "\n\\h4r $d\n";
    $ww=0;
    $od=$d;
  }

  $ww+=$w;
  if ($ww>$max_width) {
    print OUT "\n";
    $ww=$w;
  }
  print OUT "\\photo $p\n";
}

print OUT "</body></html>\n";
close OUT;

`addphoto "$file" > "$dir.htm"`;
